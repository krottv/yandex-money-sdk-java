apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.yandex.java'

ext.isTeamcityBuild = project.hasProperty('teamcity')
ext.isMavenCentralBuild = project.hasProperty('mavenCentral')

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    destinationDir = file("build/outputs/")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
    destinationDir = file("build/outputs/")
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    archives file: file("build/outputs/ym-java-epr-sdk-" + version + ".pom")
}

project.afterEvaluate {

    if (isMavenCentralBuild || isTeamcityBuild) {
        signing {
            project.ext["signing.keyId"] = teamcity['signing.keyId']
            project.ext["signing.password"] = teamcity['signing.password']
            project.ext["signing.secretKeyRingFile"] = teamcity['signing.secretKeyRingFile']
            sign configurations.archives
        }
    } else {
        task signArchives {
            // do nothing
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    parent {
                        groupId 'org.sonatype.oss'
                        artifactId 'oss-parent'
                        version '7'
                    }
                    modelVersion '4.0.0'
                    groupId 'com.yandex.java'
                    artifactId 'ym-java-epr-sdk'
                    version version
                    name 'Yandex.Money SDK for Java'
                    description 'This Java library contains classes that allows you to do payments using Yandex.Money public API.'
                    url 'https://tech.yandex.ru/money/'

                    licenses {
                        license {
                            name 'Agreement on Use of the Yandex.Money API'
                            url 'https://money.yandex.ru/doc.xml?id=526828&ncrnd=1189/'
                            distribution 'repo'
                        }
                    }

                    scm {
                        connection 'scm:git://github.com/yandex-money/yandex-money-sdk-java.git'
                        developerConnection 'scm:git://github.com/yandex-money/yandex-money-sdk-java.git'
                        url 'https://github.com/yandex-money/yandex-money-sdk-java.git'
                    }


                    dependencies {
                        compile 'com.squareup.okhttp:okhttp:1.6.0'
                        compile 'com.google.code.gson:gson:2.3'
                        compile 'joda-time:joda-time:2.5'
                    }

                    developers {
                        developer {
                            name 'Yandex.Money'
                            url 'https://money.yandex.ru/'
                            roles {
                                role 'Developer'
                                role 'Contributor'
                            }
                        }
                    }
                }.writeTo("build/outputs/ym-java-epr-sdk-" + version + ".pom")

                if (isTeamcityBuild || isMavenCentralBuild) {
                    if (isMavenCentralBuild) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    }
                    project.ext['auth'] = { authentication(userName: teamcity['maven.username'], password: teamcity['maven.password']) }
                    repository(url: teamcity['maven.repository'], project.ext['auth'])
                    snapshotRepository(url: teamcity['maven.snapshotRepository'], project.ext['auth'])
                } else {
                    repository(url: "file://${System.env.HOME}/.yandex-money-sdk-repo/")
                }
            }
        }
    }
}

uploadArchives.dependsOn ':build'